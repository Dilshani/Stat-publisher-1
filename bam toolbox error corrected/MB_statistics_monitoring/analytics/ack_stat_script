CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStat
    (messageID STRING,Time_stamp BIGINT, publisherIP STRING,payload_Message_id BIGINT,payload_queueName STRING)
    STORED BY 'org.apache.hadoop.hive.cassandra.CassandraStorageHandler'
    WITH SERDEPROPERTIES (
    "wso2.carbon.datasource.name" = "WSO2BAM_CASSANDRA_DATASOURCE",
    "cassandra.cf.name" = "ACK_STATISTICS_MB" ,
    "cassandra.columns.mapping" =
    ":key, Timestamp, meta_publisherIP,payload_messageId, payload_queueName" );
    
CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerMinute
     (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,hour SMALLINT,minute SMALLINT, time STRING)
      STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES (
      'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
      'hive.jdbc.update.on.duplicate' = 'true',
      'hive.jdbc.primary.key.fields' = 'publisherIP,year,month,day,hour,minute',
      'hive.jdbc.table.create.query' = 'CREATE TABLE ACKNOWLEDGE_STATS_SUMMARY_PER_MINUTE (publisherIP VARCHAR(100) NOT NULL,payload_queueName VARCHAR,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,hour SMALLINT,minute SMALLINT,time VARCHAR(30))');

insert overwrite table MBAcknowledgeStatPerMinute
select publisherIP,payload_queueName,COUNT(payload_Message_id) as ackPacket_count,
year(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )) as year, month(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )) as month,day(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )) as day,hour(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )) as hour, minute(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )) as minute, 
concat(substring(from_unixtime(cast(Time_stamp/1000 as BIGINT), 'yyyy-MM-dd HH:mm:ss'),0,16),':00') as time
from MBAcknowledgeStat
group by publisherIP,payload_queueName,
year(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )), month(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )),day(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )),hour(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )),minute(from_unixtime(cast(Time_stamp/1000 as BIGINT),'yyyy-MM-dd HH:mm:ss.SSS' )),substring(from_unixtime(cast(Time_stamp/1000 as BIGINT), 'yyyy-MM-dd HH:mm:ss'),0,16);
                            
CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerMinuteDataFetcher (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,hour SMALLINT,time STRING) 
STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES ( 
'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
'mapred.jdbc.input.table.name' = 'ACKNOWLEDGE_STATS_SUMMARY_PER_MINUTE' );
  
CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerHour
     (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,hour SMALLINT,time STRING)
      STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES (
      'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
      'hive.jdbc.update.on.duplicate' = 'true',
      'hive.jdbc.primary.key.fields' = 'publisherIP,year,month,day,hour',
      'hive.jdbc.table.create.query' = 'CREATE TABLE ACKNOWLEDGE_STATS_SUMMARY_PER_HOUR (publisherIP VARCHAR(100) NOT NULL,payload_queueName VARCHAR,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,hour SMALLINT,time VARCHAR(30))');

insert overwrite table MBAcknowledgeStatPerHour
select publisherIP,payload_queueName,SUM(ackPacket_count) as ackPacket_count,
year,month,day,hour, 
concat(substr(time,0,13),':00:00') as time
from MBAcknowledgeStatPerMinuteDataFetcher
group by  publisherIP,payload_queueName,
year,month,day,hour,substr(time,0,13);

CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerHourDataFetcher (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,hour SMALLINT,time STRING) 
STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES ( 
'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
'mapred.jdbc.input.table.name' = 'ACKNOWLEDGE_STATS_SUMMARY_PER_HOUR' ); 

CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerDay
     (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,time STRING)
      STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES (
      'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
      'hive.jdbc.update.on.duplicate' = 'true',
      'hive.jdbc.primary.key.fields' = 'publisherIP,year,month,day',
      'hive.jdbc.table.create.query' = 'CREATE TABLE ACKNOWLEDGE_STATS_SUMMARY_PER_DAY (publisherIP VARCHAR(100) NOT NULL,payload_queueName VARCHAR,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,time VARCHAR(30))');

insert overwrite table MBAcknowledgeStatPerDay
select publisherIP,payload_queueName,SUM(ackPacket_count) as ackPacket_count,
year,month,day,substr(time,0,10) as time
from MBAcknowledgeStatPerHourDataFetcher
group by publisherIP,payload_queueName,year,month,day,substr(time,0,10);


CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerDayDataFetcher 
       (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,day SMALLINT,time STRING) 
       STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES ( 
      'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
      'mapred.jdbc.input.table.name' = 'ACKNOWLEDGE_STATS_SUMMARY_PER_DAY' );     
      
CREATE EXTERNAL TABLE IF NOT EXISTS MBAcknowledgeStatPerMonth
     (publisherIP STRING,payload_queueName STRING,ackPacket_count INT,year SMALLINT,month SMALLINT,time STRING)
      STORED BY 'org.wso2.carbon.hadoop.hive.jdbc.storage.JDBCStorageHandler' TBLPROPERTIES (
      'wso2.carbon.datasource.name'='WSO2BAM_DATASOURCE',
      'hive.jdbc.update.on.duplicate' = 'true',
      'hive.jdbc.primary.key.fields' = 'publisherIP,year,month',
      'hive.jdbc.table.create.query' = 'CREATE TABLE ACKNOWLEDGE_STATS_SUMMARY_PER_MONTH (publisherIP VARCHAR(100) NOT NULL,payload_queueName VARCHAR,ackPacket_count INT,year SMALLINT,month SMALLINT,time VARCHAR(30))');

insert overwrite table MBAcknowledgeStatPerMonth
select  publisherIP,payload_queueName,SUM(ackPacket_count) as ackPacket_count,year,month,substr(time,0,7) as time
from MBAcknowledgeStatPerDayDataFetcher
group by publisherIP,payload_queueName,year,month,substr(time,0,7);
      
